---
layout:     post
title:      "Cmake常见使用总结"
date:       2016-09-19
author:     "Ryan"
header-img: "img/post-bg-2015.jpg"
tags:
    - C/C++
    - cmake
    - Programming
---

## 1. `CMakeLists.txt` 基本框架模板

```cmake
cmake_minimum_required(VERSION 3.0)

project(myproject)

# The version number: 2.3.4
set(MY_VERSION_MAJOR 2)
set(MY_VERSION_MINOR 3)
set(MY_VERSION_PATCH 4)

option(WITH_LIBXML2 "Enable using libxml2" ON)

# configure a header file to pass some of the CMake settings
# to the source code
configure_file (
  "${PROJECT_SOURCE_DIR}/config.h.in"
  "${PROJECT_BINARY_DIR}/config.h"
)

# add the binary tree to the search path for include files
# so that we will find TutorialConfig.h
include_directories("${PROJECT_BINARY_DIR}")

set(LIB_SUFFIX "64" CACHE STRING "Optional arch-dependent suffix for the library installation directory")
set(CMAKE_INSTALL_PREFIX "/usr")

set(RUNTIME_INSTALL_DIR bin
    CACHE PATH "Install dir for executables and dlls")
set(ARCHIVE_INSTALL_DIR ${CMAKE_INSTALL_PREFIX}/lib${LIB_SUFFIX}
    CACHE PATH "Install dir for static libraries")
set(LIBRARY_INSTALL_DIR ${CMAKE_INSTALL_PREFIX}/lib${LIB_SUFFIX}
    CACHE PATH "Install dir for shared libraries")
set(INCLUDE_INSTALL_DIR ${CMAKE_INSTALL_PREFIX}/include
    CACHE PATH "Install dir for headers")
set(PACKAGE_INSTALL_DIR lib${LIB_SUFFIX}/cmake
    CACHE PATH "Install dir for cmake package config files")
set(SBIN_INSTALL_DIR ${CMAKE_INSTALL_PREFIX}/sbin
    CACHE PATH "Install dir for sbin")

# set(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake/modules)

set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -W -Wall -fPIC -std=c++0x")

# set(CMAKE_BUILD_TYPE "Debug")
# set(CMAKE_BUILD_TYPE "Release")
```

`config.h.in`
```CPP
#ifndef VERSION_H
#define VERSION_H

#define MY_VERSION_MAJOR @MY_VERSION_MAJOR@
#define MY_VERSION_MINOR @MY_VERSION_MINOR@
#define MY_VERSION_PATCH @MY_VERSION_PATCH@

#cmakedefine WITH_LIBXML2 1

#endif
```

# 2. 常用命令和关键字

## 2.1 `set`与`option`

设置`cmake`中的变量, 搭配`configure_file`命令对`config.h.in`配置文件生成相应的`config.h`
```cmake
set(CMAKE_BUILD_TYPE "Debug")
set(SOLR_ROOT "/opt/solr/solr-6.1.0" CACHE STRING "solr root directory")
option(ENABLE_STATIC "Compile static library" OFF)
```

## 2.2 找库,找程序,找文件

### 2.2.1 `find_package`和`pkg_config`

`find_package`使用cmake module中的文件找到相应的库以及设置相应的环境变量.
`pkg_check_modules`使用传统的pkg_config工具来寻找并配置库所需的参数.
`include_directories`设置额外的头文件搜索路径,如`gcc`的参数`-I/usr/include/libxml2`
`link_directories`设置额外的库搜索路径,如`gcc`的参数`-L/usr/local/lib`
```cmake
find_package(PkgConfig REQUIRED)
# set(PKG_CONFIG_PATH "${PKG_CONFIG_PATH}:${MYLIB_ROOT}/lib/pkgconfig")

pkg_check_modules(LIBXML2 REQUIRED libxml-2.0)
include_directories(${LIBXML2_INCLUDE_DIRS})
link_directories(${LIBXML2_LIBRARY_DIRS})
```

### 2.2.2 `find_library`
对于没有提供`pkg_config`的`pc`文件以及也没有提供`cmake module`的库来说可以通过`find_library`来寻找库,从而确定相应的库是否已经安装.
`message`可以用来打印屏幕输出消息.`SEND_ERROR`指示出错并停止`cmake`继续执行.

```cmake
find_library(MAGIC_LIB magic)
if(NOT MAGIC_LIB)
  message(SEND_ERROR "magic library not found")
else()
  message(STATUS "Found library : ${MAGIC_LIB}")
endif()
```

### 2.2.3 其他

* find_file
* find_path
* find_program

### 2.3 `add_library`和`add_executable`

`add_library`和`add_executable`都是用来生成目标.但是`add_library`用来生成库或者`OBJECT`,`add_executable`用来生成可执行程序.
`target_link_libraries`用来链接库.
```cmake
set(myproject_SRC
  src1.cc
  src2.cc
)
set(myproject_LIBS
  ${LIBXML2_LIBRARIES}
)
if(WITH_JSONCPP)
  set(myproject_SRC ${myproject_SRC}
    src_use_jsoncpp.cc
  )
  set(myproject_LIBS ${myproject_LIBS}
    ${JSONCPP_LIBRARIES}
  )
endif()

add_library(myproject SHARED ${myproject_SRC})
target_link_libraries(myproject ${myproject_LIBS})

add_executable(mymain main.cc)
target_link_libraries(mymain myproject)
```

## 2.4 `install`

`install`用来指定`make install`的安装规则.
* `TARGETS`用来安装目标
* `FILES`用来安装普通文件
* `PROGRAMS`用来安装可执行程序

```cmake
install(TARGETS file_parse
  RUNTIME DESTINATION ${RUNTIME_INSTALL_DIR}
  LIBRARY DESTINATION ${LIBRARY_INSTALL_DIR}
  ARCHIVE DESTINATION ${ARCHIVE_INSTALL_DIR}
)

set(myproject_HEADERS
  src1.h
  src2.h
)
set(myproject_HEADERS_INSTALL_DIR "${INCLUDE_INSTALL_DIR}/myproject")
install(FILES ${myproject_HEADERS}
  DESTINATION ${myproject_HEADERS_INSTALL_DIR}
)

install(PROGRAMS start.sh
  DESTINATION ${SBIN_INSTALL_DIR}
  RENAME myproject_start.sh
  COMPONENT runtime
)
```

## 2.5 `execute_process`

如果需要执行系统命令或脚本,甚至对输出进行处理,这就需要用到`execute_process`命令了
```cmake
execute_process(COMMAND <cmd1> [args1...]]
                [COMMAND <cmd2> [args2...] [...]]
                [WORKING_DIRECTORY <directory>]
                [TIMEOUT <seconds>]
                [RESULT_VARIABLE <variable>]
                [OUTPUT_VARIABLE <variable>]
                [ERROR_VARIABLE <variable>]
                [INPUT_FILE <file>]
                [OUTPUT_FILE <file>]
                [ERROR_FILE <file>]
                [OUTPUT_QUIET]
                [ERROR_QUIET]
                [OUTPUT_STRIP_TRAILING_WHITESPACE]
                [ERROR_STRIP_TRAILING_WHITESPACE])
```

## 2.6 `cmake`更多命令

https://cmake.org/cmake/help/v3.0/manual/cmake-commands.7.html

# 3. `mingw`交叉编译`Toolchain.cmake`

```bash
cmake -DCMAKE_TOOLCHAIN_FILE=Toolchain.cmake ..
```

`Toolchain.cmake`

```cmake
# the name of the target operating system
SET(CMAKE_SYSTEM_NAME Windows) 

# for 32 or 64 bits mingw-w64
set(COMPILER_PREFIX "i686-w64-mingw32")
#set(COMPILER_PREFIX "x86_64-w64-mingw32"

# which compilers to use for C and C++
find_program(CMAKE_C_COMPILER NAMES ${COMPILER_PREFIX}-gcc)
#SET(CMAKE_C_COMPILER ${COMPILER_PREFIX}-gcc)
find_program(CMAKE_CXX_COMPILER NAMES ${COMPILER_PREFIX}-g++)
#SET(CMAKE_CXX_COMPILER ${COMPILER_PREFIX}-g++)
find_program(CMAKE_RC_COMPILER NAMES ${COMPILER_PREFIX}-windres)
#SET(CMAKE_RC_COMPILER ${COMPILER_PREFIX}-windres)
find_program(CMAKE_AR NAMES ${COMPILER_PREFIX}-ar)
#SET(CMAKE_AR ${COMPILER_PREFIX}-ar)
find_program(CMAKE_LINKER NAMES ${COMPILER_PREFIX}-ld)
#SET(CMAKE_LINKER ${COMPILER_PREFIX}-ld)
find_program(CMAKE_RANLIB NAMES ${COMPILER_PREFIX}-ranlib)
#SET(CMAKE_RANLIB ${COMPILER_PREFIX}-ranlib)

# adjust the default behaviour of the FIND_XXX() commands:
# search headers and libraries in the target environment, search 
# programs in the host environment
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)

# here is the target environment located
SET(CMAKE_FIND_ROOT_PATH  /usr/${COMPILER_PREFIX}/sys-root/mingw/)
SET(CMAKE_INSTALL_PREFIX  /usr/${COMPILER_PREFIX}/sys-root/mingw/)
```

